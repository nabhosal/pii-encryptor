plugins {
    id 'java'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'io.github.nabhosal'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.9'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.13'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.7'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'io.github.nabhosal'
            artifactId 'pii-encryptor'
            version '1.0-SNAPSHOT'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}


def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Utility for encrypting & decryption field level PII (i.e Personally identifiable information) in document. Useful utility for implementing in-house Payment Card Industry ( PCI ) or similar  compliance.'
            name 'PII-Encryptor'
            url 'https://github.com/nabhosal/pii-encryptor'
            organization {
                name 'io.github.nabhosal'
                url 'https://github.com/nabhosal'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/nabhosal/pii-encryptor/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/nabhosal/pii-encryptor/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/nabhosal/pii-encryptor'
                connection 'scm:git:git://github.com/nabhosal/pii-encryptor.git'
                developerConnection 'scm:git:ssh://git@github.com:nabhosal/pii-encryptor.git'
            }
            developers {
                developer {
                    name 'nabhosal'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}